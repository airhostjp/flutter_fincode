group 'co.airhost.flutter_fincode'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.7.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

String localMavenPath = project.mkdir("build").absolutePath
allprojects {
    repositories {
        google()
        mavenCentral()
        rootProject.allprojects {
            repositories {
                maven { url "file://$localMavenPath" }
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 34
    namespace 'co.airhost.flutter_fincode'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
    }
}

dependencies {
    String dartDefines = ''
    String appEnv = ''
    System.getenv().each { k, v ->
        if (k == 'APP_ENV') {
            appEnv = v
        }
    }
    if (project.hasProperty('dart-defines')) {
        try {
            dartDefines = new String(project.property('dart-defines').decodeBase64(), 'UTF-8').replaceAll(' ', '').toUpperCase()
        } catch (Exception e) {
        }
    }
    boolean isStage = dartDefines.contains("APP_ENV=STAGE") || appEnv.toUpperCase() == 'STAGE'
    println('Fincode Env: ' + (isStage ? 'Stage' : 'Prod'))
    if (isStage) {
        implementation files('libs/stage.jar')
    } else {
        implementation files('libs/prod.jar')
    }
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.9.0'
}